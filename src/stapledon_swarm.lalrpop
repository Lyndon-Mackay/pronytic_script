
use rust_decimal::Decimal;
use crate::stapledon_swarm::{Field,StapledonSwarmData,StapledonToken};

use crate::common::GoodConsumes;

grammar;

extern {
    type Location = usize;
    type Error = String;

    enum StapledonToken {
        "string" => StapledonToken::String(<String>),
        "number" => StapledonToken::Number(<u8>),
        "decimal_number" => StapledonToken::DecimalNumber(<Decimal>),

        
        "=" => StapledonToken::Equal,
        ":" => StapledonToken::Colon,

        "{" => StapledonToken::LeftCurly,
        "}" => StapledonToken::RightCurly,

        "[" => StapledonToken::LeftSquare,
        "]" => StapledonToken::RightSquare,

        "name" => StapledonToken::Name,

        "swarm_asset" => StapledonToken::SwarmAsset,
        "receiver_asset" => StapledonToken::ReceiverAsset,

        
        "good_id" => StapledonToken::GoodId,

        "amount" => StapledonToken::Amount,
        
        "consumes" => StapledonToken::Consumes,
        "upkeep" => StapledonToken::Upkeep,

        
        "time" => StapledonToken::Time,
        "power" => StapledonToken::Power,
    }
    
}

pub StapledonData:Vec<StapledonSwarmData> = {
    <list:StapledonDatum*> =>list,
}

StapledonDatum:StapledonSwarmData = {
    <level:"number">  "=" "{" <fields:StapledonField*> "}" =>{
        let mut stapledon = StapledonSwarmData{
            level,
            ..Default::default()
        };
        for f in fields {
            match f {
                Field::Name(n) => stapledon.name = n,
                Field::SwarmAsset(a) => stapledon.swarm_asset = a,
                Field::ReceiverAsset(a) => stapledon.receiver_asset = a,
                Field::Cost(c) => stapledon.costs = c,
                Field::Upkeep(c) => stapledon.upkeep = c,
                Field::Time(t) => stapledon.time = t,
                Field::Power(p) => stapledon.power = p,
            }
        }
        stapledon
    }
}

StapledonField:Field = {
    <n:Name> => Field::Name(n),
    <a:SwarmAsset> => Field::SwarmAsset(a),
    <a:ReceiverAsset> => Field::ReceiverAsset(a),
    <c:GoodConsumes> => Field::Cost(c),
    <c:GoodUpkeep> => Field::Upkeep(c),
    <p:Power> => Field::Power(p),
    <t:Time> => Field::Time(t),
}

Name:String = {
    "name" "=" <s:"string"> => s,
}

Time:u8 = {
    "time" "=" <t:"number"> => t,
}

Power:Decimal = {
    "power" "=" <p:"decimal_number"> => p,
}

SwarmAsset:String = {
    "swarm_asset" "=" <s:"string"> => s,
}

ReceiverAsset:String = {
    "receiver_asset" "=" <s:"string"> => s,
}

GoodConsumes:Vec<GoodConsumes> = {
    "consumes" "=" "[" <c:GoodConsume*> "]" =>c,
}

GoodUpkeep:Vec<GoodConsumes> = {
    "upkeep" "=" "[" <c:GoodConsume*> "]" =>c,
}


GoodConsume:GoodConsumes = {
    "{" "good_id" ":" <id:"string"> "amount" ":" <n:"decimal_number"> "}" => {
        GoodConsumes {
            id,
            amount:n,
        }
    }
}

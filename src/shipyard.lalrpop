use rust_decimal::prelude::*;

use crate::shipyard::{Field,ShipyardData,ShipyardToken};

use crate::common::GoodConsumes;

grammar;

extern {
    type Location = usize;
    type Error = String;

    enum ShipyardToken {
        "true" => ShipyardToken::True,
        "false" => ShipyardToken::False,
        "string" => ShipyardToken::String(<String>),
        "number" => ShipyardToken::Number(<u8>),
        "decimal_number" => ShipyardToken::DecimalNumber(<Decimal>),

        "=" => ShipyardToken::Equal,
        ":" => ShipyardToken::Colon,

        "{" => ShipyardToken::LeftCurly,
        "}" => ShipyardToken::RightCurly,

        "[" => ShipyardToken::LeftSquare,
        "]" => ShipyardToken::RightSquare,

        "name" => ShipyardToken::Name,
        "asset_location" => ShipyardToken::AssetLocation,

        "good_id" => ShipyardToken::GoodId,

        "amount" => ShipyardToken::Amount,

        "consumes" => ShipyardToken::Consumes,

        "time" => ShipyardToken::Time,

        "star_class" => ShipyardToken::StarClass,

        "armaments" => ShipyardToken::Armaments,

        "base_strength" => ShipyardToken::BaseStrength,
        "fleet_strength" => ShipyardToken::FleetStrength,

    }
}

pub ShipyardData:Vec<ShipyardData> = {
    <list:ShipyardDatum*> => list,
}

ShipyardDatum:ShipyardData = {
    <level:"number"> "=" "{" <fields:ShipyardField*> "}" => {
        let mut shipyard = ShipyardData{
            level,
            ..Default::default()
        };
        for f in fields{
            match f {
                Field::Name(n) => shipyard.name = n,
                Field::AssetLocation(a) => shipyard.asset_location = a,
                Field::Consumes(c) => shipyard.costs = c,
                Field::Time(t) => shipyard.time = t,
                Field::BaseStrength(b) => shipyard.base_strength = b,
                Field::FleetStrength(f) => shipyard.fleet_strength = f,
                Field::StarClass(s) => shipyard.star_class = s,
                Field::Armaments(a) => shipyard.armaments = a,
            }
        }

        shipyard
    }
}


ShipyardField:Field ={
    <n:Name> => Field::Name(n),
    <a:AssetLocation> => Field::AssetLocation(a),
    <c:GoodConsumes> => Field::Consumes(c),
    <t:Time> => Field::Time(t),
    <b:BaseStrength> => Field::BaseStrength(b),
    <f:FleetStrength> => Field::FleetStrength(f),
    <s:StarClass> => Field::StarClass(s),
    <a:Armaments> => Field::Armaments(a),
}

Bool:bool = {
    "true" => true,
    "false" => false,
}

Name:String = {
    "name" "=" <s:"string"> => s,
}

AssetLocation:String = {
    "asset_location" "=" <s:"string"> => s,
}

Time:u8 = {
    "time" "=" <t:"number"> => t,
}

GoodConsumes:Vec<GoodConsumes> = {
    "consumes" "=" "[" <c:GoodConsume*> "]" =>c,
}

GoodConsume:GoodConsumes = {
    "{" "good_id" ":" <id:"string"> "amount" ":" <n:"decimal_number"> "}" => {
        GoodConsumes {
            id,
            amount:n,
        }
    }
}

BaseStrength:Decimal = {
    "base_strength" "=" <d:"decimal_number"> => {
        d
    }
}

FleetStrength:Decimal = {
    "fleet_strength" "=" <d:"decimal_number"> => {
        d
    }
}

StarClass:bool = {
    "star_class" "=" <b:Bool> => b,
}

Armaments:bool = {
    "armaments" "=" <b:Bool> => b,
}

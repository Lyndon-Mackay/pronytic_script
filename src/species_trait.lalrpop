use rust_decimal::Decimal;

use crate::species_trait::{Effect,Field,SpeciesTraitData,SpeciesToken};
use crate::common::GoodConsumes;


grammar;

extern {
    type Location = usize;
    type Error = String;

    enum SpeciesToken {
        "string" => SpeciesToken::String(<String>),
        "number" => SpeciesToken::DecimalNumber(<Decimal>),

        "name" => SpeciesToken::Name,
        "icon" => SpeciesToken::Icon,

        "=" => SpeciesToken::Equal,
        ":" => SpeciesToken::Colon,

        "{" => SpeciesToken::LeftCurly,
        "}" => SpeciesToken::RightCurly,

        "[" => SpeciesToken::LeftSquare,
        "]" => SpeciesToken::RightSquare,

        
        "good_id" => SpeciesToken::GoodId,

        "amount" => SpeciesToken::Amount,
        
        "consumes" => SpeciesToken::Consumes,

		"effects" => SpeciesToken::Effects,

        "growth_rate" => SpeciesToken::GrowthRate,
    }
	
}

pub SpeciesTraits:Vec<SpeciesTraitData> = {
    <list:SpeciesTrait*> => list,
}

SpeciesTrait:SpeciesTraitData = {
    
    <id:Id> <fields:Field*> => {
        let mut name = "".to_string();
        let mut icon = "".to_string();
        let mut consumes = Vec::new();
        let mut effects = Vec::new();

        for f in fields.into_iter(){
            match f {
                Field::Name(n) => name = n,
                Field::Icon(i) => icon = i,
                Field::Consumes(c) => consumes = c,
                Field::Effects(e) => effects = e,
            }
        }

        SpeciesTraitData {
            id,
            name,
            icon,
            consumes,
            effects,
        }
    }
}


Field:Field = {
    <n:Name> => Field::Name(n),
    <i:Icon> => Field::Icon(i),
    <c:GoodConsumes> => Field::Consumes(c),
    <e:Effects> => Field::Effects(e),
}

Id:String = {
    "string"
}

Name:String = {
    "name" "=" <id:"string"> => id,
}

Icon:String = {
    "icon" "=" <id:"string"> => id,
}

GoodConsumes:Vec<GoodConsumes> = {
    "consumes" "=" "[" <c:GoodConsume*> "]" => c,
}

GoodConsume:GoodConsumes = {
    "{" "good_id" ":" <id:"string"> "amount" ":" <n:"number">  "}" => {
        GoodConsumes {
            id,
            amount:n,
        }    
    }
}

Effects:Vec<Effect> = {
  "effects"  "{" <effect:Effect*> "}" => effect
}

Effect:Effect = {
    "growth_rate" "=" <n:"number"> => Effect::GrowthRate(n),
}

use crate::ranks::{Field,RankData,RankToken};

use rust_decimal::prelude::*;

grammar;


extern {
	type Location = usize;
	type Error = String;


	enum RankToken{
		"string" => RankToken::String(<String>),
		"number" => RankToken::Number(<u16>),
		"decimal" => RankToken::DecimalNumber(<Decimal>),

		"=" => RankToken::Equal,
		"{" => RankToken::LeftCurly,
		"}" => RankToken::RightCurly,

		"number_of_stars" => RankToken::NumStars,
		"stockpile_max" => RankToken::StockpileMax,
		"huck_max" => RankToken::HuckMax,

		"level" => RankToken::Level,
		"name" => RankToken::Name,
		"description" => RankToken::Description,
	}
}

pub RankData:Vec<RankData> = {
	<list:RankDatum*> => list,
}

RankDatum:RankData = {
	<level:"number"> "=" "{" <fields:RankField*>"}" =>{
		let mut rank = RankData{
			level,..Default::default()
		};
		for f in fields{
			match f {
				Field::Name(n) => rank.name = n,
				Field::NumStars(n) => rank.number_of_stars = n,
				Field::StockpileMax(n) => rank.stockpile_max = n,
				Field::HuckMax(d) => rank.huck_max = d,
				Field::Description(d) => rank.description = Some(d),
			}
		}

		rank
	}
}


RankField:Field ={
	<n:Name> => Field::Name(n),
	<n:NumStars> => Field::NumStars(n),
	<n:StockpileMax> => Field::StockpileMax(n),
    <d:HuckMax> => Field::HuckMax(d),
	<d:Description> => Field::Description(d),
}

Name:String = {
	"name" "=" <s:"string"> => s,
}

NumStars:u16 = {
	"number_of_stars" "=" <n:"number"> => n,
}

StockpileMax:u16 = {
	"stockpile_max" "=" <n:"number"> => n,
}

HuckMax:Decimal = {
	"huck_max" "=" <d:"decimal"> => d,
}

Description:String = {
	"description" "=" <s:"string"> => s,
}

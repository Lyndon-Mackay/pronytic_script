use rust_decimal::prelude::*;

use crate::orbital::{Field,OrbitalData,OrbitalToken};

use crate::common::GoodConsumes;

grammar;

extern {
    type Location = usize;
    type Error = String;

    enum OrbitalToken{
        "string" => OrbitalToken::String(<String>),
        "number" => OrbitalToken::Number(<u8>),
        "decimal_number" => OrbitalToken::DecimalNumber(<Decimal>),

        "=" => OrbitalToken::Equal,
        ":" => OrbitalToken::Colon,

        "{" => OrbitalToken::LeftCurly,
        "}" => OrbitalToken::RightCurly,

        "[" => OrbitalToken::LeftSquare,
        "]" => OrbitalToken::RightSquare,

        "name" => OrbitalToken::Name,
        "asset_location" => OrbitalToken::AssetLocation,

        "good_id" => OrbitalToken::GoodId,

        "amount" => OrbitalToken::Amount,
        
        "consumes" => OrbitalToken::Consumes,

        "time" => OrbitalToken::Time,
        "building_limit" => OrbitalToken::BuildingLimit,
    }

}

pub OrbitalData:Vec<OrbitalData> = {
    <list:OrbitalDatum*> => list,
}

OrbitalDatum:OrbitalData = {
    <level:"number"> "=" "{" <fields:OrbitalField*> "}" => {
        let mut orbital = OrbitalData{
            level,
            ..Default::default()
        };
        for f in fields {
            match f {
                Field::Name(n) => orbital.name = n,
                Field::AssetLocation(a) => orbital.asset_location = a,
                Field::Consumes(c) => orbital.costs = c,
                Field::Time(t) => orbital.time = t,
                Field::BuildingLimit(l) => orbital.building_limit = l,
            }
        }
        orbital
    }
}

OrbitalField:Field ={
    <n:Name> => Field::Name(n),
    <a:AssetLocation> => Field::AssetLocation(a),
    <c:GoodConsumes> => Field::Consumes(c),
    <t:Time> => Field::Time(t),
    <l:BuildingLimit> => Field::BuildingLimit(l),
}
Name:String = {
    "name" "=" <s:"string"> => s,
}

AssetLocation:String = {
    "asset_location" "=" <s:"string"> => s,
}

Time:u8 = {
    "time" "=" <t:"number"> => t,
}

BuildingLimit:u8 = {
    "building_limit" "=" <l:"number"> => l,
}

GoodConsumes:Vec<GoodConsumes> = {
    "consumes" "=" "[" <c:GoodConsume*> "]" =>c,
}

GoodConsume:GoodConsumes = {
    "{" "good_id" ":" <id:"string"> "amount" ":" <n:"decimal_number"> "}" => {
        GoodConsumes {
            id,
            amount:n,
        }
    }
}




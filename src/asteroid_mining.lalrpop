use rust_decimal::Decimal;
use crate::asteroid_mining::{Field,AsteroidMiningData,Token};

use crate::common::GoodConsumes;

grammar;

extern {
    type Location = usize;
    type Error = String;

    enum Token{
        "string" => Token::String(<String>),
        "number" => Token::Number(<u8>),
        "decimal_number" => Token::DecimalNumber(<Decimal>),

        "=" => Token::Equal,
        ":" => Token::Colon,

        "{" => Token::LeftCurly,
        "}" => Token::RightCurly,

        "[" => Token::LeftSquare,
        "]" => Token::RightSquare,

        "name" => Token::Name,
        "depot_asset" => Token::DepotAsset,
        "ship_asset" => Token::ShipAsset,

        "good_id" => Token::GoodId,

        "amount" => Token::Amount,
        
        "consumes" => Token::Consumes,
        "produces" => Token::Produces,

        "power" => Token::Power,

        "time" => Token::Time,
        
    }
}

pub AsteroidMiningData:Vec<AsteroidMiningData> = {
    <list:AsteroidMiningDatum*> => list,
}

AsteroidMiningDatum:AsteroidMiningData = {
    <level:"number"> "=" "{" <fields:AsteroidField*>  "}" => {
        let mut asteroid_mine = AsteroidMiningData::default();
        asteroid_mine.level = level;

        for f in fields {
            match f {
                Field::Name(n) => asteroid_mine.name = n,
                Field::DepotAsset(n) => asteroid_mine.depot_asset = n,
                Field::ShipAsset(n) => asteroid_mine.ship_asset = n,
                Field::Consumes(c) => asteroid_mine.costs = c,
                Field::Produces(c) => asteroid_mine.produces = c,
                Field::Time(t) => asteroid_mine.time = t,
                Field::Power(p) => asteroid_mine.power = p,
            }
        }
        asteroid_mine
    }
}

AsteroidField:Field ={
    <n:Name> => Field::Name(n),
    <a:DepotAsset> => Field::DepotAsset(a),
    <a:ShipAsset> => Field::ShipAsset(a),
    <c:GoodConsumes> => Field::Consumes(c),
    <c:GoodProduces> => Field::Produces(c),
    <t:Time> => Field::Time(t),
    <p:Power> => Field::Power(p),
}

Name:String = {
    "name" "=" <s:"string"> => s,
}

DepotAsset:String = {
    "depot_asset" "=" <s:"string"> =>s,
}

ShipAsset:String = {
    "ship_asset" "=" <s:"string"> =>s,
}

Time:u8 = {
    "time" "=" <t:"number"> => t,
}

Power:Decimal = {
    "power" "=" <p:"decimal_number"> => p,
}

GoodProduces:Vec<GoodConsumes> = {
    "produces" "=" "[" <c:GoodConsume*> "]" =>c,
}
GoodConsumes:Vec<GoodConsumes> = {
    "consumes" "=" "[" <c:GoodConsume*> "]" =>c,
}

GoodConsume:GoodConsumes = {
    "{" "good_id" ":" <id:"string"> "amount" ":" <n:"decimal_number"> "}" => {
        GoodConsumes {
            id,
            amount:n,
        }
    }
}
